#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Lighting.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Deferred.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Geometry.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/ImageBasedLighting.hlsl"

#pragma kernel Compute
#pragma kernel Temporal
#pragma kernel Spatial
#pragma kernel Combine
#pragma enable_d3d11_debug_symbols

RWTexture2D<float4> _VisibilityCone;
RWTexture2D<float> _FrameCountResult;
RWTexture2D<uint> _Result;
Texture2D<float3> _PreviousFrame;
Texture2D<float2> _Motion;
Texture2D<float> _Depth, _PreviousDepth, _FrameCountPrevious, _FrameCount;
Texture2D<uint> _Input, _History;

cbuffer AOConstants
{
	float4 _UvToView;
	float _SampleCount, _Strength, _ThinOccluderCompensation, _MaxScreenRadius, _FalloffScale, _FalloffBias, _Radius, _SampleDistributionPower, _DepthMipSamplingOffset, _MaxMips, _ClampWindowScale, _DepthRejection, _VelocityRejection, _ClampVelocityWeight, _AccumFrameCount, _DirectionCount, _WorldRadius;
	float _DistanceWeight, _NormalWeight, _TangentWeight;
	uint _BlurSamples;
	uint _MaxWidth, _MaxHeight;
};

// Inputs are screen XY and viewspace depth, output is viewspace position
float3 ComputeViewspacePosition(float2 screenPos, float viewspaceDepth)
{
	float3 ret;
	ret.xy = (screenPos * _ScreenSize.zw * _UvToView.xy + _UvToView.zw) * viewspaceDepth;
	ret.z = viewspaceDepth;
	return ret;
}

uint PackOutput(float4 input)
{
	input /= 1.5;
	input.xyz = 0.5 * input.xyz + 0.5;
	uint4 result = uint4(saturate(input) * 255.0 + 0.5);
	return result.x | (result.y << 8) | (result.z << 16) | (result.w << 24);
}

float4 UnpackInput(uint input)
{
	float4 output = (input >> uint4(0, 8, 16, 24)) & 0xFF;
	output = output / 255.0;
	output.xyz = 2.0 * output.xyz - 1.0;
	return output * 1.5;
}

[numthreads(8, 8, 1)]
void Compute(uint2 id : SV_DispatchThreadID)
{
	float2 pixelCenter = floor(id * 2 + 1 + _Jitter * _ScreenSize.xy) + 0.5;
	
	float3 N = GBufferNormal(pixelCenter);
	float3 normalV = WorldToViewDir(N, true);
	float2 noise = _BlueNoise2D[id % 128];

	float viewspaceZ = LinearEyeDepth(_Depth[pixelCenter], _ZBufferParams);
	float3 cPosV = ComputeViewspacePosition(pixelCenter, viewspaceZ);
	float3 viewV = normalize(-cPosV);

	float screenspaceRadius = min(_MaxScreenRadius, _Radius / viewspaceZ);
	float ratio = saturate(_MaxScreenRadius / (_Radius / viewspaceZ));
	
	float4 result = 0.0;
	for (float i = 0; i < _DirectionCount; i++)
	{
		float phi = PI / _DirectionCount * (i + noise.x);
		float3 directionV = float3(cos(phi), sin(phi), 0.0);
		
		float3 orthoDirectionV = ProjectOnPlane(directionV, viewV);
		float3 axisV = normalize(cross(directionV, viewV));
		float3 projNormalV = ProjectOnPlane(normalV, axisV);
	
		float sgnN = sign(dot(orthoDirectionV, projNormalV));
		float cosN = saturate(dot(projNormalV, viewV) / length(projNormalV));
		float n = sgnN * FastACos(cosN);
		
		float2 h;
		
		[unroll]
		for (uint side = 0; side < 2; side++)
		{
						// Find the intersection with the next pixel, and use that as the starting point for the ray
			float2 direction = directionV.xy * (2.0 * side - 1.0);
			float2 cell = floor(pixelCenter);
			float2 tMin = (cell - pixelCenter) / direction;
			float2 tMax = (cell - pixelCenter + 1.0) / direction;
			float t = Max2(max(tMin, tMax));
			float2 start = pixelCenter + direction * t;
			float2 end = pixelCenter + direction * screenspaceRadius;
			float2 step = (end - start) / _SampleCount;
			
			float lowHorizonCos = cos(n + (2.0 * side - 1.0) * HALF_PI);
			float cHorizonCos = lowHorizonCos;
			
			for (float j = 0.0; j < _SampleCount; j++)
			{
				float2 position = start + (j + noise.y) * step;
				float SZ = LinearEyeDepth(_Depth[position], _ZBufferParams);
				float3 sPosV = ComputeViewspacePosition(position, SZ);
				float3 sHorizonV = normalize(sPosV - cPosV);

				// Falloff
				float start = _WorldRadius * 0.75 * ratio;
				float end = _WorldRadius * ratio;
				float weight = saturate((end - distance(sPosV, cPosV)) / (end - start));
				float sHorizonCos = lerp(lowHorizonCos, dot(sHorizonV, viewV), weight);
			
				if (sHorizonCos >= cHorizonCos)
				{
					// If weighted horizon is greater than the previous sample, it becomes the new horizon
					cHorizonCos = sHorizonCos;
				}
				else if (dot(sHorizonV, viewV) < cHorizonCos)
				{
					// Otherwise, reduce the max horizon to attenuate thin features, but only if the -non- weighted sample is also below the current sample
					// This prevents the falloff causing objects to be treated as thin when they would not be otherwise
					cHorizonCos = max(lowHorizonCos, cHorizonCos - _ThinOccluderCompensation);
				}
			}

			h[side] = n + clamp((2.0 * side - 1.0) * FastACos(cHorizonCos) - n, -HALF_PI, HALF_PI);
			result.w += length(projNormalV) * (cosN + 2.0 * h[side] * sin(n) - cos(2.0 * h[side] - n)) / 4.0;
		}
		
		// see "Algorithm 2 Extension that computes bent normals b."
		float t0 = (6 * sin(h[0] - n) - sin(3 * h[0] - n) + 6 * sin(h[1] - n) - sin(3 * h[1] - n) + 16 * sin(n) - 3 * (sin(h[0] + n) + sin(h[1] + n))) / 12;
		float t1 = (-cos(3 * h[0] - n) - cos(3 * h[1] - n) + 8 * cos(n) - 3 * (cos(h[0] + n) + cos(h[1] + n))) / 12;
	
		float3 bentNormalL = normalize(float3(directionV.x * t0, directionV.y * t0, -t1));
		result.xyz += mul(RotFromToMatrix(float3(0, 0, -1), viewV), bentNormalL) * length(projNormalV);
	}
	
	result /= _DirectionCount;
		
	result.xyz = ViewToWorldDir(result.xyz, false);
	_Result[id] = PackOutput(result);
}

[numthreads(8, 8, 1)]
void Temporal(uint2 id : SV_DispatchThreadID)
{
	float2 uv = (id + 0.5) * _ScreenSize.zw;
	uint4 input = _Input.Gather(_PointClampSampler, uv).xywz;
	
	float4 result = 0.0, mean = 0.0, stdDev = 0.0;
	float maxWeight = 0.0, weightSum = 0.0;
	
	[unroll]
	for (uint y = 0, i = 0; y < 2; y++)
	{
		[unroll]
		for (uint x = 0; x < 2; x++, i++)
		{
			float4 color = UnpackInput(input[i]);
			float2 fullResCoord = (((id - 1) >> 1) + int2(x, y)) * 2 + 1 + _Jitter * _ScreenSize.xy;
			float2 delta = abs(fullResCoord - id + 0.5);
			float weight = saturate(1.0 - delta.x) * saturate(1.0 - delta.y);
			
			maxWeight = max(maxWeight, weight);
			weightSum += weight;
			result += color * weight;
			mean += color;
			stdDev += color * color;
		}
	}
	
	if(weightSum > 0.0)
		result /= weightSum;
	
	mean /= 4.0;
	stdDev = sqrt(abs(stdDev / 4.0 - mean * mean));

	float accumSpeed = 0.0;
	float2 velocity = _Motion[id];
	float2 previousUv = uv - velocity;
	if (all(saturate(previousUv) == previousUv))
	{
		// Sample history, remove weighting from history and result, blend, re-apply weight
		uint4 packedHistory = _History.Gather(_PointClampSampler, previousUv);
		float4 currentDepths = LinearEyeDepth(_Depth.Gather(_PointClampSampler, uv));
		float4 previousDepths = LinearEyeDepth(_PreviousDepth.Gather(_PointClampSampler, previousUv));
		float4 accumSpeedPrev = _FrameCountPrevious.Gather(_PointClampSampler, previousUv) * 255.0;
		float4 bilinearWeights = BilinearWeights(previousUv, _ScreenSize.xy);
	
		float distanceToPoint = LinearEyeDepth(_Depth[id], _ZBufferParams);
		
		float4 history = 0.0;
	
		[unroll]
		for (i = 0; i < 4; i++)
		{
			float bilateralWeight = saturate(1.0 - (currentDepths[i] - previousDepths[i]) * _DepthRejection / distanceToPoint);
			float4 historySample = UnpackInput(packedHistory[i]);
			history += bilinearWeights[i] * lerp(result, historySample, bilateralWeight);
			accumSpeed += bilinearWeights[i] * bilateralWeight * accumSpeedPrev[i];
		}
	
		accumSpeed = min(accumSpeed, _AccumFrameCount);
		float speed = 1.0 / (1.0 + accumSpeed * 0.25);
	
		float velocityWeight = saturate(1.0 - length(velocity) * _ClampVelocityWeight);
		float4 extents = stdDev * (1.0 + _ClampWindowScale * velocityWeight);
		history = clamp(history, mean - extents, mean + extents);
	
		float t = speed * maxWeight;
		result = lerp(history, result, t);
	}
	
	_Result[id] = PackOutput(result);
	_FrameCountResult[id] = (accumSpeed + 1.0) / 255.0;
}

[numthreads(8, 8, 1)]
void Spatial(uint2 id : SV_DispatchThreadID)
{
	float frameCount = _FrameCount[id] * 255.0;
	float phi = _BlueNoise1D[id % 128] * TWO_PI;
	float depth = _Depth[id];
	float3 positionWS = PixelToWorld(id + 0.5, depth);
 
	float4 result = UnpackInput(_Input[id]);
	float weightSum = 1.0;
	
	float3 normal = GBufferNormal(id);
	float3x3 frame = GetLocalFrame(normal);
	
	float rcpEyeDepth = rcp(LinearEyeDepth(depth, _ZBufferParams));
	float ratio = saturate(_MaxScreenRadius / (_Radius * rcpEyeDepth));
	float radius = _WorldRadius * ratio * (1.0 / (1.0 + frameCount));
	
	// Tangent plane
	float4 plane = float4(normal, -dot(normal, positionWS));
	
	for (uint i = 0; i < _BlurSamples; i++)
	{
		float3 p = float3(radius * VogelDiskSample(i, _BlurSamples, phi, 2.0), 0.0);
		float3 samplePosition = mul(p, frame) + positionWS;
		float2 pixel = floor(WorldToPixel(samplePosition, false)) + 0.5;
		
		if (any(pixel < 0.0 || pixel >= _ScreenSize.xy))
			continue;
		
		float sampleDepth = _Depth[pixel];
		float3 sampleWorldPos = PixelToWorld(pixel + 0.5, sampleDepth);
		float3 sampleNormal = GBufferNormal(pixel);
		float weight = saturate(1.0 - abs(dot(plane, float4(sampleWorldPos, 1.0))) * _TangentWeight * rcpEyeDepth);
		weight *= saturate(1.0 - Angle(normal, sampleNormal) * _NormalWeight * rcpEyeDepth);
		
		result += UnpackInput(_Input[pixel]) * weight;
		weightSum += weight;
	}
	
	result /= weightSum;
	
	_Result[id] = PackOutput(result);
}

[numthreads(8, 8, 1)]
void Combine(uint2 id : SV_DispatchThreadID)
{
	float2 uv = (id + 0.5) * _ScreenSize.zw + _Jitter;
	uint4 inputs = _Input.Gather(_PointClampSampler, uv);
	float4 weights = BilinearWeights(uv, _ScreenSize.xy);
	
	float4 result = 0.0;
	
	[unroll]
	for (uint i = 0; i < 4; i++)
		result += UnpackInput(inputs[i]) * weights[i];
	
	// Remove weighting from final result
	result /= length(result.rgb);
	
	// Apply final modifier after temporal to reduce variance
	result.a = pow(result.a, _Strength);
	
	float4 visibilityCone = _VisibilityCone[id];
	visibilityCone.xyz = GBufferNormal(visibilityCone);
	visibilityCone = BlendVisibiltyCones(visibilityCone, result);
	visibilityCone = result;
	
	_VisibilityCone[id] = PackGBufferNormal(visibilityCone.xyz, visibilityCone.w);
}