#pragma kernel Intersect
//#pragma kernel Temporal
//#pragma kernel Upsample

#pragma multi_compile _ VOXEL_GI_ON

#define SSR_TRACE_EPS               0.000488281f // 2^-11, should be good up to 4K

#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Deferred.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/GGX.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/GGXExtensions.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Lighting.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/ReflectionProbe.hlsl"
#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/Utility.hlsl"

RWTexture2D<float> _HitResult, _FrameCountResult;
RWTexture2D<float4> _Result;
Texture2D<float4> _HitInput, _Input, _History;
Texture2D<float2> _MotionVectors;
Texture2D<float> _PrevFrameCount, _Depth, _FrameCountPrevious, _PreviousDepth;
float4x4 _PixelCoordToViewDirWS;
float2 _Resolution, _ResolutionMinusOne;
float _Thickness, _BrdfBias, _StdDevFactor, _Sharpness, _BlurRadius;
uint _MaxMip, _MaxSteps;

float FFX_SSSR_LoadDepth(int2 current_mip_position, int current_mip)
{
	return _Depth.mips[current_mip][current_mip_position];
}

float3 FFX_SSSR_LoadWorldSpaceNormal(int2 pixel_coordinate)
{
	return GBufferNormal(pixel_coordinate);
}

float3 FFX_SSSR_ScreenSpaceToViewSpace(float3 screen_space_position)
{
	screen_space_position.y = (1 - screen_space_position.y);
	screen_space_position.xy = 2 * screen_space_position.xy - 1;
	float4 projected = mul(_InvViewProjMatrix, float4(screen_space_position, 1));
	projected.xyz /= projected.w;
	return MultiplyPoint3x4(_ViewMatrix, projected.xyz);
}

#define FFX_SSSR_INVERTED_DEPTH_RANGE

#include "Packages/com.arycama.noderenderpipeline/ShaderLibrary/ffx_sssr.hlsl"

float3 InvProjectPosition(float3 coord, float4x4 mat)
{
	//coord.y = (1 - coord.y);
	coord.xy = 2 * coord.xy - 1;
	float4 projected = mul(mat, float4(coord, 1));
	projected.xyz /= projected.w;
	return projected.xyz;
}

float3 ScreenSpaceToWorldSpace(float3 screen_space_position)
{
	return InvProjectPosition(screen_space_position, _InvViewProjMatrix);
}

float3 ScreenToWorld(float3 position)
{
	return MultiplyPointProj(_InvViewProjMatrix, float3(position.xy * 2.0 - 1.0, position.z)).xyz;
}

float3 SampleGGXReflection(float3 i, float2 alpha, float2 rand)
{
	float3 i_std = normalize(float3(i.xy * alpha, i.z));
	// Sample a spherical cap
	float phi = 2.0 * Pi * rand.x;
	float a = saturate(min(alpha.x, alpha.y)); // Eq. 6
	float s = 1.0f + length(float2(i.x, i.y)); // Omit sgn for a <=1
	float a2 = a * a;
	float s2 = s * s;
	float k = (1.0 - a2) * s2 / (s2 + a2 * i.z * i.z); // Eq. 5
	float b = i.z > 0 ? k * i_std.z : i_std.z;
	float z = mad(1.0 - rand.y, 1.0 + b, -b);
	float sinTheta = sqrt(saturate(1.0 - z * z));
	float3 o_std = { sinTheta * cos(phi), sinTheta * sin(phi), z };
	// Compute the microfacet normal m
	float3 m_std = i_std + o_std;
	float3 m = normalize(float3(m_std.xy * alpha, m_std.z));
	// Return the reflection vector o
	return 2.0 * dot(i, m) * m - i;
}

//float GGXReflectionPDF ( float3 i , float3 o , float2 alpha ) {
//float3 m = normalize ( i + o ) ;
//float ndf = D (m , alpha ) ;
//float2 ai = alpha * i . xy ;
//float len2 = dot( ai , ai ) ;
//float t = sqrt ( len2 + i . z * i . z ) ;
//if ( i . z >= 0.0 f ) {
//float a = saturate (min( alpha .x, alpha .y)); // Eq. 6
//float s = 1.0f + length ( float2 (i.x, i.y)); // Omit sgn for a <=1
//float a2 = a * a; float s2 = s * s;
//float k = (1.0 f - a2) * s2 / (s2 + a2 * i.z * i.z); // Eq. 5
//return ndf / (2.0 f * (k * i . z + t ) ) ; // Eq. 8 * || dm/do ||
//}
//// Numerically stable form of the previous PDF for i.z < 0
//return ndf * ( t - i . z ) / (2.0 f * len2 ) ; // = Eq. 7 * || dm/do ||
//}

[numthreads(8, 8, 1)]
void Intersect(uint2 id : SV_DispatchThreadID)
{
	float depth = _Depth[id];
	if (depth == _FarClipValue)
	{
		_Result[id] = 0;
		_HitResult[id] = 0;
		return;
	}
	
	float2 u = _BlueNoise2D[id % 128];
	float3 V = MultiplyVector(_PixelCoordToViewDirWS, float3(id + 0.5, 1.0), true);
	float3 N = GBufferNormal(id);
	
	float2 roughness = Sq(GBufferPerceptualRoughness(id));

    // Compute the reflection direction
	float3x3 localToWorld = GetLocalFrame(N);
	float3 localR = SampleGGXReflection(mul(localToWorld, V), roughness, u);
	float3 R = mul(localR, localToWorld);
  
    // We start tracing from the center of the current pixel, and do so up to the far plane.
	float3 positionWS = ScreenToWorld(float3((id + 0.5) / _Resolution, depth));
	
	float3 rayOrigin = float3((id + 0.5) / _Resolution, depth);
	float3 reflPosSS = MultiplyPointProj(_ViewProjMatrix, positionWS + R);
	reflPosSS.xy = 0.5 * reflPosSS.xy + 0.5;
	reflPosSS.y = 1.0 - reflPosSS.y;
	
	float3 rayDir = reflPosSS - rayOrigin;

	bool validHit;
	float3 hit = FFX_SSSR_HierarchicalRaymarch(rayOrigin, rayDir, true, _ScreenSize.xy, 0, 0, _MaxSteps, validHit);
	
	float3 world_space_origin = ScreenSpaceToWorldSpace(rayOrigin);
	float3 world_space_hit = ScreenSpaceToWorldSpace(hit);
	float3 world_space_ray = world_space_hit - world_space_origin.xyz;
	
	float2 uv = (id + 0.5) / _Resolution;
	float confidence = validHit ? FFX_SSSR_ValidateHit(hit, uv, world_space_ray, _ScreenSize.xy, _Thickness * 100) : 0;

	if (confidence > 0.0)
	{
		_HitResult[id] = length(world_space_ray);
		
		float2 prevPos = WorldToNdcPrevious(world_space_hit).xy * _ScreenSize.xy;
		_Result[id] = float4(_Input[prevPos].rgb, 1);
		
		//_Result[id] = float4(_Input[hit.xy * _ScreenSize.xy].rgb, 1);
	}
	else
	{
		_HitResult[id] = 0.0;
		_Result[id] = 0.0;
	}
}

[numthreads(8, 8, 1)]
void Temporal(uint2 id : SV_DispatchThreadID)
{
	float4 mean = 0.0, stdDev = 0.0;
	
	[unroll]
	for (uint y = 0; y < 3; y++)
	{
		[unroll]
		for (uint x = 0; x < 3; x++)
		{
			uint2 coord = clamp(id + uint2(x, y) - 1, 0, _Resolution);
			
			float4 colorSample = _Input[coord];
			colorSample.rgb /= 1.0 + Luminance(colorSample.rgb);
			
			mean += colorSample;
			stdDev += Sq(colorSample);
		}
	}
	
	mean /= 9.0;
	stdDev = sqrt(abs(stdDev / 9.0 - Sq(mean)));
	
	float2 velocity = _MotionVectors[id << 1];
	
	float hitData = _HitInput[id];
	float depth = _Depth.mips[1][id];
	float3 positionWS = ScreenToWorld(float3((id + 0.5) / floor(_ScreenSize.xy / 2.0), depth));
	float3 V = MultiplyVector(_PixelCoordToViewDirWS, float3((id + 0.5) / floor(_ScreenSize.xy / 2.0) * _ScreenSize.xy, 1.0), true);
	float3 worldHitPosition = positionWS - V * hitData;
	float2 previousUv = WorldToNdcPrevious(worldHitPosition).xy * _Resolution;
	
	float2 previousCoord = floor(previousUv - 0.5);
	float2 previousFrac = frac(previousUv - 0.5);
	
	float4 history = 0.0;
	float weightSum = 0.0, accumSpeed = 0.0;
	float currentDepth = Linear01ToDeviceDepth(Linear01Depth(_Depth.mips[1][id])); // TODO: THis is redundant
	
	float3 Xprev = MultiplyPointProj(_PrevInvViewProjMatrix, float3((previousUv / _Resolution) * 2 - 1, currentDepth)).xyz;
	float invDistToPoint = rcp(LinearEyeDepth(_Depth.mips[1][id]));
	
	float _DepthRejection = 5.0, _VelocityRejection = 10.0;
	
	[unroll]
	for (y = 0; y < 2; y++)
	{
		[unroll]
		for (int x = 0; x < 2; x++)
		{
			float2 coord = clamp(previousCoord + float2(x, y), 0.0, _ResolutionMinusOne);
			
			float previousDepth = _PreviousDepth.mips[1][coord];
			float3 previousPositionWS = MultiplyPointProj(_PrevInvViewProjMatrix, float3(((coord + 0.5) / _Resolution) * 2 - 1, previousDepth)).xyz;
			
			float bilinearWeight = abs(x - 1.0 + previousFrac.x) * abs(y - 1.0 + previousFrac.y);
			float weight = saturate(1.0 - abs(distance(Xprev, previousPositionWS)) * _DepthRejection * invDistToPoint);
			weight *= saturate(1.0 - abs(length(velocity.xy)) * _VelocityRejection);
			weight *= bilinearWeight;
			
			float4 historySample = _History[coord];
			historySample.rgb /= 1.0 + Luminance(historySample.rgb);
			history += historySample * weight;
			weightSum += weight;
			
			accumSpeed += _FrameCountPrevious[coord] * weight;
		}
	}
	
	if (weightSum > 0.0)
		history /= weightSum;
	
	float _AccumFrameCount = 32.0;
	float _MinStdDev = 1.5;
	float _MaxStdDev = 3.5;
	float _StdDevFrameCount = 32.0;
	
	accumSpeed *= 255.0;
	accumSpeed = min(accumSpeed + 1.0, _AccumFrameCount);
	float speed = 1.0 / (1.0 + accumSpeed);
	
	// Expand bounding box based on accumulated frame count
	float factor = lerp(_MinStdDev, _MaxStdDev, saturate(accumSpeed / _StdDevFrameCount));
	float4 minValue = mean - stdDev * factor;
	float4 maxValue = mean + stdDev * factor;
	history = clamp(history, minValue, maxValue);
	
	float4 result = _Input[id];
	result.rgb /= 1.0 + Luminance(result.rgb);
	result = lerp(history, result, speed);
	result.rgb /= 1.0 - Luminance(result.rgb);
	
	_Result[id] = result;
	_FrameCountResult[id] = accumSpeed / 255.0;
}
